# Maintainer: David Arroyo <droyo@aqwari.us>
pkgname=skalibs-musl
pkgver=1.3.0
pkgrel=1
pkgdesc="A set of general-purpose C programming libraries"
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/skalibs"
license=('custom:ISC')
changelog=CHANGELOG
makedepends=(musl)
provides=(skalibs)
source=(${url}/skalibs-$pkgver.tar.gz)
md5sums=('25865de0e545fe29fa80a68314fddb38')

build() {
  cd "$srcdir/prog/skalibs-$pkgver"
  echo musl-gcc                 > conf-compile/conf-cc
  echo musl-gcc -static         > conf-compile/conf-ld
  echo musl-gcc                 > conf-compile/conf-dynld
  echo /usr/bin                 > conf-compile/conf-install-command
  echo /usr/lib/skalibs         > conf-compile/conf-install-library
  echo /usr/include/skalibs     > conf-compile/conf-install-include
  echo /usr/lib/skalibs/sysdeps > conf-compile/conf-install-sysdeps
  rm -f conf-compile/flag-slashpackage
  rm -f conf-compile/flag-allstatic
  package/compile
}

package() {
  cd "$srcdir/prog/skalibs-$pkgver"
  for i in package/*.exported 
  do 
    case `basename $i` in
    library.so.exported) d=/lib                    ;;
    include.exported)    d=/usr/include/skalibs    ;;
    sysdeps.exported)    d=/usr/lib/skalibs/sysdeps;;
    library.exported)    d=/usr/lib/skalibs        ;;
    command.exported)    d=/usr/bin                ;;
    esac
    f=`basename $i|sed 's/.exported//'`
    mkdir -p "$pkgdir/$d"
    install -D `sed s,^,$f/, $i` "$pkgdir/$d"
  done
  install -D -m644 etc/leapsecs.dat "$pkgdir/etc/leapsecs.dat"
  install -D -m644 package/COPYING "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
