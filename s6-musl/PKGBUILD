# Maintainer: David Arroyo <droyo@aqwari.us>
pkgname=s6-musl
pkgver=1.0.0
pkgrel=1
pkgdesc="skarnet.org's small & secure supervision software suite"
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/s6"
license=('custom:ISC')
changelog=CHANGELOG
makedepends=(skalibs execline musl)
provides=(s6)
source=(${url}/s6-$pkgver.tar.gz)
md5sums=('e83c8189a78423bb0143db18f09537f8')

build() {
  cd "$srcdir/admin/s6-$pkgver"
  echo musl-gcc                 > conf-compile/conf-cc
  echo musl-gcc -static         > conf-compile/conf-ld
  echo musl-gcc                 > conf-compile/conf-dynld
  echo /usr/bin                 > conf-compile/conf-install-command
  echo /usr/lib                 > conf-compile/conf-install-library
  echo /lib                     > conf-compile/conf-install-library.so
  echo /usr/include             > conf-compile/conf-install-include
  echo /usr/lib/skalibs/sysdeps > conf-compile/import
  echo /usr/include/skalibs     > conf-compile/path-include
  echo /usr/include/execline    >>conf-compile/path-include
  echo /usr/lib/skalibs         > conf-compile/path-library
  echo /usr/lib/execline        >>conf-compile/path-library
  rm -f conf-compile/flag-slashpackage
  rm -f conf-compile/flag-allstatic
  package/compile
}

package() {
  cd "$srcdir/admin/s6-$pkgver"
  for i in package/*.exported 
  do 
    case `basename $i` in
    library.so.exported) d=/lib         ;;
    include.exported)    d=/usr/include ;;
    library.exported)    d=/usr/lib     ;;
    command.exported)    d=/usr/bin     ;;
    esac
    f=`basename $i|sed 's/.exported//'`
    mkdir -p "$pkgdir/$d"
    install -D `sed s,^,$f/, $i` "$pkgdir/$d"
  done
  install -D -m755 command/s6-ftrigrd "$pkgdir/usr/bin/s6-ftrigrd"
  install -D -m644 package/COPYING "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
