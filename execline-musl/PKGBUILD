# Maintainer: David Arroyo <droyo@aqwari.us>
pkgname=execline-musl
pkgver=1.2.0
pkgrel=3
pkgdesc="A small scripting language, to be used in place of a shell in non-interactive scripts"
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/execline"
license=('custom:ISC')
changelog=CHANGELOG
makedepends=(skalibs musl)
provides=(execline)
source=("${url}/execline-$pkgver.tar.gz" 'execline.eb' 'execlineb.eb' 'fdreserve.patch')
md5sums=('d21b609bc471b0f271f54fc928d5ba87'
         '416d62013cf0ce4b4f24085649ed4fb7'
         '6c47998acdb22dab7137cf94c1be22a1'
         'cb29f770fa49cf9ceaa6ab94db5a2858')

build() {
  cd "$srcdir/admin/execline-$pkgver"
  patch -p1 < $srcdir/fdreserve.patch
  echo musl-gcc                 > conf-compile/conf-cc
  echo musl-gcc -static         > conf-compile/conf-ld
  echo musl-gcc                 > conf-compile/conf-dynld
  echo /usr/lib/skalibs/sysdeps > conf-compile/import
  echo /usr/libexec/execline    > conf-compile/conf-install-command
  echo /usr/lib/execline        > conf-compile/conf-install-library
  echo /usr/include/execline    > conf-compile/conf-install-include
  echo /usr/include/skalibs     > conf-compile/path-include
  echo /usr/lib/skalibs         > conf-compile/path-library
  rm -f conf-compile/flag-slashpackage
  rm -f conf-compile/flag-allstatic
  package/compile
}

package() {
  cd "$srcdir/admin/execline-$pkgver"
  for i in package/*.exported 
  do 
    case `basename $i` in
    library.so.exported) d=lib                    ;;
    include.exported)    d=usr/include/execline   ;;
    library.exported)    d=usr/lib/execline       ;;
    command.exported)    d=usr/libexec/execline   ;;
    esac
    f=`basename $i|sed 's/.exported//'`
    mkdir -p $pkgdir/$d
    install `sed s,^,$f/, $i` "$pkgdir/$d"
  done
  install -D -m755 "$srcdir/execlineb.eb" "$pkgdir/usr/bin/execlineb"
  install -D -m755 "$srcdir/execline.eb"  "$pkgdir/usr/bin/execline"
  install -D -m644 package/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
